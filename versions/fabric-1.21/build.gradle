import ottermc.Joiner

plugins {
	id 'fabric-loom' version "${loom_version}"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"modid" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	implementation files("libs/client.jar")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

tasks.named("build") {
	def dependencies = ["fabric-1.21.8-patch"]
	doLast {
		def src = new File(project.layout.buildDirectory.getAsFile().get(), "libs" + File.separator + "omc-1.0.0.jar")
		dependencies.each {
			Joiner.joinJars(src, [String.join(File.separator, [project.projectDir.parent, it, "build", "libs", "omc-1.0.0.jar"])])
		}
	}
	dependsOn(dependencies.collect {":versions:" + it +":build"})
}